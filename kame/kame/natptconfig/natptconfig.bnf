/*	$KAME: natptconfig.bnf,v 1.6 2001/09/02 20:49:10 fujisawa Exp $	*/

/*
 * Copyright (C) 1995, 1996, 1997, 1998, 1999, 2000 and 2001 WIDE Project.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 * 3. Neither the name of the project nor the names of its contributors
 *    may be used to endorse or promote products derived from this software
 *    without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE PROJECT AND CONTRIBUTORS ``AS IS'' AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED.  IN NO EVENT SHALL THE PROJECT OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/* Top level definitions */

  <statement> ::= <question>
		| <break>
		| <prefix>
		| <rules>
		| <switch>
		| <set>
		| <show>
		| <log>
		| <test>
		| <error>


/* question mark */

   <question> ::= SQUESTION


/* stop at breakpoint (for debug) */

      <break> ::= SBREAK


/* set NAT-PT prefix */

     <prefix> ::= SPREFIX SQUESTION
		| SPREFIX <ipv6addr>


/* Translation rule */

      <rules> ::= SMAP SQUESTION

		/* IPv6 -> IPv4 */
		| SMAP SFROM SANY6 STO <ipv4addr> <opt_ports> <opt_proto>
		| SMAP SFROM <ipv6addrs> STO <ipv4addrs> <opt_ports> <opt_proto>

		/* IPv4 -> IPv6 */
		| SMAP SFROM <ipv4addrs> STO <ipv6addrs> <opt_proto>
		| SMAP SFROM <ipv4addr> <dport> STO <ipv6addr> <dport> <opt_proto>

		/* IPv4 -> IPv4 (outbound) */
		| SMAP SFROM <ipv4addrs> STO <ipv4addrs> <opt_ports> <opt_proto> <opt_bidir>

		/* IPv4 -> IPv4 (inbound) */
		| SMAP SFROM <ipv4addr> <dport> STO <ipv4addr> <dport> <opt_proto>

		| SMAP SFLUSH <opt_all>


/* Translation of/off */

     <switch> ::= SMAP SENABLE
		| SMAP SDISABLE


/* Set variable(s) */

	<set> ::= SSET SQUESTION
		| SSET name SEQUAL SDECIMAL
		| SSET name SEQUAL SHEXADECIMAL


/* Show rule/variables/... */

       <show> ::= SSHOW SQUESTION
		| SSHOW SPREFIX
		| SSHOW SRULES <opt_all>
		| SSHOW SXLATE <opt_long> <opt_decimal>
		| SSHOW SVARIABLES
		| SSHOW SMAPPING


/* Log */

	<log> ::= SLOG SSTRING
		| SLOG SSYSLOG


/* Test log system */
       <test> ::= STEST SQUESTION
		| STEST SLOG
		| STEST SLOG SNAME
		| STEST SLOG SSTRING


/* conventional */

  <opt_ports> ::= <eps>
		| SPORT SDECIMAL SMINUS SDECIMAL
		| SPORT SDECIMAL SCOLON SDECIMAL

  <opt_proto> ::= <eps>
		| <protos>

    <opt_all> ::= <eps>
		| SALL

  <opt_bidir> ::= <eps>
		| SBIDIR

   <opt_long> ::= <eps>
		| SLONG

<opt_decimal> ::= <eps>
		| SDECIMAL

      <dport> ::= SDPORT SDECIMAL

     <protos> ::= <proto>
		| <protos> SSLASH <proto>

      <proto> ::= SICMP
		| STCP
		| SUDP

  <ipv4addrs> ::= <ipv4addr>
		| <ipv4addr> SSLASH <SDECIMAL>
		| <ipv4addr> SMINUS <ipv4addr>

   <ipv4addr> ::= SIPV4ADDR

  <ipv6addrs> ::= <ipv6addr>
		| <ipv6addr> SSLASH SDECIMAL

   <ipv6addr> ::= SIPV6ADDR

       <name> ::= SNAME
